apply plugin: 'com.android.application'

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        exec {
            commandLine 'git', 'log', '--format="%h"', '-n 1'
            standardOutput = stdout
        }
        return stdout.toString().replace('\n', '-').replace('"', '').trim()
    }
    catch (ignored) {
        return null
    }
}

android {
    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword = RELEASE_STORE_PASSWORD
            keyAlias = RELEASE_KEY_ALIAS
            keyPassword = RELEASE_KEY_PASSWORD
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "me.erikhennig.worktracks"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 9
        versionName "1.3.2"

        buildConfigField "String", "VERSION_NAME_FULL", "\"${getVersionName()}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.preference:preference:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    annotationProcessor 'androidx.room:room-compiler:2.2.5'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.navigation:navigation-fragment:2.2.1'
    implementation 'androidx.navigation:navigation-ui:2.2.1'
    implementation 'androidx.room:room-runtime:2.2.5'
    implementation 'com.google.android.material:material:1.1.0'
    testImplementation 'androidx.room:room-testing:2.2.5'
    testImplementation 'junit:junit:4.12'
    implementation 'com.github.ravindu1024:android-keyboardlistener:1.0.0'
    implementation 'com.opencsv:opencsv:5.1'
}
